pluginManagement {
	repositories {
		maven {
			name = 'Fabric'
			url = 'https://maven.fabricmc.net/'
		}
		maven {
			name = 'Jitpack'
			url = 'https://maven.fallenbreath.me/jitpack'
		}
		mavenCentral()
		gradlePluginPortal()
	}
	resolutionStrategy {
		eachPlugin {
			switch (requested.id.id) {
				case "com.replaymod.preprocess": {
					useModule("com.github.Fallen-Breath:preprocessor:${requested.version}")
					break
				}
			}
		}
	}
}

for (File version : new File('./versions').listFiles()) {
	if (version.isDirectory()) {
		include(":${version.name}")

		def proj = project(":${version.name}")
		proj.projectDir = file("versions/${version.name}")
		proj.buildFileName = "../../common.gradle"
	}
}
